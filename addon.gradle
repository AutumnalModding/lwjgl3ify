import org.gradle.jvm.toolchain.internal.DefaultToolchainSpec

sourceSets {
    create("util") {
        java {
        }
    }
    named("main").configure {
        java {
            srcDirs += ["src/generated/java"]
        }
    }
}

dependencies {
    utilImplementation("org.ow2.asm:asm:9.4")
    utilImplementation("org.ow2.asm:asm-tree:9.4")
    utilImplementation("org.apache.commons:commons-lang3:3.12.0")
    utilImplementation("commons-io:commons-io:2.11.0")
    utilImplementation("commons-collections:commons-collections:3.2.2")
    utilImplementation("com.google.guava:guava:31.1-jre")
}

tasks.register("runComparisonTool", JavaExec) {
    classpath(sourceSets.util.runtimeClasspath)
    dependsOn("jar")
    mainClass.set("me.eigenraven.lwjgl3ify.ComparisonTool")
    minHeapSize = "2G"
    maxHeapSize = "2G"
}

afterEvaluate {
    def lwjgl2Zips = configurations.named("lwjglMcCompileClasspath").get().resolve().findAll {!it.name.contains("natives") && !it.path.contains("net.java.j")}
    def lwjgl3Zips = configurations.named("lwjglModClasspath").get().resolve().findAll {!it.name.contains("natives")}
    def lwjgl2Args = lwjgl2Zips.collect {"--2:" + it}
    def lwjgl3Args = lwjgl3Zips.collect {"--3:" + it}
    def allArgs = lwjgl2Args + lwjgl3Args + ["--M:" + tasks.jar.archiveFile.get().asFile.path]
    tasks.named("runComparisonTool", JavaExec).configure {
        args(allArgs)
    }
}

def java11ToolchainSpec = new DefaultToolchainSpec(objects)
java11ToolchainSpec.vendor.set(JvmVendorSpec.ADOPTIUM)
java11ToolchainSpec.languageVersion.set(JavaLanguageVersion.of(11))
def java11Launcher = javaToolchains.launcherFor(java11ToolchainSpec)

for (runTask in ["runClient", "runServer", "runObfClient", "runObfServer"]) {
    tasks.named(runTask, JavaExec).configure {
        javaLauncher.set(java11Launcher)
        jvmArgs(
            "--add-opens", "java.base/jdk.internal.loader=ALL-UNNAMED",
            "--add-opens", "java.base/java.net=ALL-UNNAMED",
        )
    }
}
